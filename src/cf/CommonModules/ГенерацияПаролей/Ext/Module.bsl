#Область ПрограммныйИнтерфейс

// Генерация нового пароля
//
// Параметры:
//  ДлинаПароля			 - Число - длина пароля 
//  ПараметрыГенерации	 - Структура - см. метод ШаблонПараметровГенерации
// 
// Возвращаемое значение:
//  Строка - новый пароль  
//
Функция НовыйПароль(Знач ДлинаПароля = 5, ПараметрыГенерации = Неопределено) Экспорт
	// TODO: переиспользовать потом ПользователиСлужебный.СоздатьПароль?
	ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел();
	Результат = "";
	Если ПараметрыГенерации = Неопределено Тогда	
		Алфавит = Алфавит(ШаблонПараметровГенерации());
	Иначе
		Алфавит = Алфавит(ПараметрыГенерации);	
	КонецЕсли;
	ДлинаАлфавита = СтрДлина(Алфавит);
	Пока СтрДлина(Результат) < ДлинаПароля Цикл
		СлучайноеЧисло = ГенераторСлучайныхЧисел.СлучайноеЧисло(1, ДлинаАлфавита);
		СлучайныйСимвол = Сред(Алфавит, СлучайноеЧисло, 1);
		Результат = Результат + СлучайныйСимвол;
	КонецЦикла;
	Возврат Результат;	
КонецФункции

// Шаблон параметров генерации
// 
// Возвращаемое значение:
//  Структура - шаблон для генерации пароля 
//
Функция ШаблонПараметровГенерации() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("НижнийРегистр", Истина); 
	Параметры.Вставить("ВерхнийРегистр", Истина);
	Параметры.Вставить("Цифры", Истина);
	Параметры.Вставить("Знаки", Ложь);
	Возврат Параметры;
КонецФункции

// Функция - Длина пароля по умолчанию
// 
// Возвращаемое значение:
//  Число - Длина пароля из Константа.ДлинаПароляПоУмолчанию 
//
Функция ДлинаПароляПоУмолчанию() Экспорт
	Возврат ГенерацияПаролейСерверПовтИсп.ДлинаПароляПоУмолчанию();
КонецФункции

#КонецОбласти

#Область СлужебныйИнтерфейс

Функция Алфавит(ПараметрыГенерации)
	Возврат ИспользуемыеСимволы(ПараметрыГенерации.НижнийРегистр, ПараметрыГенерации.ВерхнийРегистр, 
		ПараметрыГенерации.Цифры, ПараметрыГенерации.Знаки);	
КонецФункции

Функция ИспользуемыеСимволы(НижнийРегистр = Истина, ВерхнийРегистр = Истина, Цифры = Истина, Знаки = Ложь)
	Алфавит = "";
	Если Знаки Тогда
		Алфавит = Алфавит + ":<=>?@;[\]^_`";	
	КонецЕсли;
	Если Цифры Тогда
		Алфавит = Алфавит + "01243456789";	
	КонецЕсли;
	Если НижнийРегистр Тогда
		Алфавит = Алфавит + "abcdefghijklmnopqrstuvwxyz";	
	КонецЕсли;
	Если ВерхнийРегистр Тогда
		Алфавит = Алфавит + "ABCDEFGHIJKLMNOPQRSTUVWXYZ";	
	КонецЕсли;
	Возврат Алфавит;
КонецФункции

#КонецОбласти